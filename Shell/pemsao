#!/data/oir/IRAF2.14.1/iraf/bin.redhat/ecl.e -f
# This script runs IRAF RVSAO.PEMSAO from a Unix command line
# Replace the path in the first line with one appropriate to your machine

# October 26, 2012
# By Jessica Mink

string uprm, hm, tempcom

# Set machine type appropriately
set arch=".redhat"

# Set the terminal type.
if (envget("TERM") == "sun") {
    stty gterm
} else if (envget("TERM") == "xterm") {
    stty xterm
} else if (envget("TERM") == "network") {
    stty vt100
} else {
    stty xterm
}

# Set the IRAF home and parameter directories
if (defvar("irafhome")) {
    hm = envget("irafhome")
    i = strlen(hm)
    if (substr(hm, i, i) == "/")  
        set (home = hm)
    else
        set (home = hm // "/")
    set uparm = "home$uparm/"
    }
else {
    hm = envget ("HOME")
    set (home = hm // "/IRAF/")
    set uparm = "home$uparm/"
    }

if (defvar("UPARM")) {
    uprm = envget("UPARM")
    i = strlen(uprm)
    if (substr(uprm, i, i) == "/")  
        set (uparm = uprm)
    else
        set (uparm = uprm // "/")
  }

# Set the user name which may appear on graphical output
if (envget("USER") != "") {
    set user = envget("USER")
    }

if (args == "") {
    print ("PEMSAO: Fit emission line redshifts in a spectrum")
    print ("usage: pemsao spectrum [templates=xxx][other arguments]")
    print ("       pemsao help  for IRAF help")
    print ("       pemsao dpar  to dump parameters")
    print ("       pemsao epar  to edit parameters")
    print ("       pemsao lpar  to list parameters")
    }
else if (args == "help") {
    tempcom = mktemp ("temppemsao")
    print ("help pemsao", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Edit pemsao parameters using epar
else if (args == "epar") {
    tempcom = mktemp ("temppemsao")
    print ("rvsao motd-\nepar pemsao", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Dump pemsao parameters using dpar
else if (args == "dpar") {
    tempcom = mktemp ("temppemsao")
    print ("rvsao motd-\ndpar pemsao", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# List pemsao parameters using lpar
else if (args == "lpar") {
    tempcom = mktemp ("temppemsao")
    print ("rvsao motd-\nlpar pemsao", > tempcom)
    cl (< tempcom)
    delete (tempcom)
    }

# Execute pemsao using the arguments on the command line
else {
    tempcom = mktemp ("temppemsao")
    printf ("rvsao motd-\npemsao %s\n", args, > tempcom)
    print (tempcom)
    type (tempcom)
    cl (< tempcom)
    delete (tempcom)
    }
logout
